buildscript {

    repositories {
        mavenCentral()
        mavenLocal()
        jcenter()
    }

    dependencies {
        classpath "org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}"
        classpath "se.transmode.gradle:gradle-docker:${dockerPluginVersion}"
        classpath "net.ltgt.gradle:gradle-apt-plugin:${aptPluginVersion}"
    }

}

plugins {
    id "io.spring.dependency-management" version "1.0.2.RELEASE"
}

apply from: "$rootDir/gradle/dependencies.gradle"

allprojects {
    apply plugin: "idea"

    repositories {
        mavenCentral()
        mavenLocal()
        jcenter()
        mavenRepoUrl.split(",").each { repoUrl -> maven { url repoUrl } }
    }
}

subprojects {
    apply plugin: "java"
    apply plugin: "maven"
    apply plugin: "io.spring.dependency-management"
    apply plugin: "docker"
    apply plugin: "maven-publish"
    apply plugin: "net.ltgt.apt"
    //apply plugin: 'findbugs'
    apply plugin: 'project-report'

    sourceCompatibility = 1.8

    compileJava {
        options.encoding = "UTF-8"
    }

    archivesBaseName = "oms-${it.name}"

    sourceSets {
        test {
            java.srcDirs = ["src/test/java", "src/test/groovy"]
        }
    }

    dependencies {
        compile "$appLibs.lombok"
        compile "$appLibs.commonLang3"
        compile "$appLibs.groovyAll"
        compile "$appLibs.guava"
        compile "$appLibs.immutableValue"
        compile "$appLibs.slf4j"

        apt "$appLibs.immutableValue"
        apt "$appLibs.lombok"

        testCompile "$appLibs.junit"
        testCompile "$appLibs.assertj"
        testCompile "$appLibs.spockCore"
        testCompile "$appLibs.mutabilitydetector"
    }

    dependencyManagement {
        imports {
            mavenBom "org.springframework.boot:spring-boot-starter-parent:${springBootVersion}"
            mavenBom "org.springframework.cloud:spring-cloud-dependencies:${springCloudVersion}"
        }
    }

    task buildDocker(type: Docker, dependsOn: build) {
        push = false
        applicationName = jar.baseName
        dockerfile = file("src/main/docker/Dockerfile")
        doFirst {
            copy {
                from jar
                into stageDir
            }
        }
    }

    publishing {
        publications {
            mavenJava(MavenPublication) { from components.java }
        }
    }

    jar {
        into("META-INF/maven/$project.group/$project.name") {
            from { generatePomFileForMavenJavaPublication }
            rename ".*", "pom.xml"
        }
    }

    task processImmutableValue() {

    }

    compileJava.dependsOn(processImmutableValue)

}